#! /usr/bin/perl -w

#    Tuttle --- Tiny Utility Toolkit for Tweaking Large Environments
#    Copyright (C) 2007  Smartleaf, Inc.
#
#    This program is free software; you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation; either version 2 of the License, or
#    (at your option) any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License along
#    with this program; if not, write to the Free Software Foundation, Inc.,
#    51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.

use FindBin;
use File::Spec;
use lib "${FindBin::Bin}/..";
use Tuttle::Config;
use Getopt::Std;
use strict;

my %options;
getopts ('hvn', \%options);

if ($options{h}) {
  print STDOUT <<EOF; exit

Usage: tuttle-check-all [-v] [-n] [-h] [config config config...]

This script does a consistency check on the named Tuttle
configurations (or on all Tuttle configurations that exist, if none
are explicitly named).

If there are any errors, they are described on STDOUT, and the script
exits with an error status.

Options:

  -v Also note on STDOUT which configurations had no problems.
  -n Don't check files referred to by Roles.conf, just Roles.conf itself.
  -h Check nothing; just print this message.

EOF
}

chdir "${FindBin::Bin}/.." or die "Could not chdir to ${FindBin::Bin}/..";

my $errs_found = 0;

my @configs = @ARGV;
if ($#configs < 0) {
  @configs = <*>
}
else {
  for my $dir (@configs) {
    if (! -f "$dir/Roles.conf") {
      print "Config $dir not found\n";
      ++$errs_found;
    }
  }
}

for my $dir (@configs) {
  my $file = "$dir/Roles.conf";
  if (-f $file) {
    # If -n argument is supplied, run only internal consistency checks.
    # Otherwise, check that files referred to by Roles.conf exist as well.
    eval {
      my $config = Tuttle::Config->new ($dir, $file);
      if (!$options{n}) { $config->check_full };
    };
    if ($@) {
      ++$errs_found;
      print "Error in config $dir:\n  ", $@, "\n";
    }
    elsif ($options{v}) {
      print "Config $dir OK\n"
    }
  }
}

exit ($errs_found ? 1 : 0);

